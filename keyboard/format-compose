#!/usr/bin/env perl

use strict;
use warnings;
use File::Basename qw(dirname);

our %Keysyms;
use X11::Keysyms '%Keysyms';

my %keysym = reverse %Keysyms;

local @ARGV = dirname(__FILE__) . '/compose.txt';

sub line {
  print $_[0] . "\n";
}

while (<>) {
  next if /^\s*(#.+)?$/;

  if (m!\A \s* (["'])(.+?)\1 \s+ (["'])(.+?)\3 \s* (\#.*)? \s* \z!x) {
    my ($from, $to, $comment) = ($2, $4, $5);
    my @keys =
      map { "<$_>" }
        'Multi_key',
        map { $keysym{ord $_} || $_ }
          split //, $from;
    line join ' ', grep { defined } @keys, ':', qq["$to"], $comment || "# $from";
  }
  else {
    warn "Skipping $_";
  }
}
