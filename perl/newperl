#!/usr/bin/env perl
# http://www.dagolden.com/index.php/2134/how-i-manage-new-perls-with-perlbrew/

use v5.10;
use strict;
use warnings;
use FindBin '$Bin';
use autodie qw/:all/;

my $as = shift
  or die "Usage: $0 <perl-version>";
my @args = @ARGV;

my %flag_defs = (
  t => [qw/-D usethreads/],
  s => [qw/-D useshrplib/],
  f => [qw/-Dcccdlflags=-fPIC/],
);
my %flags = map { ($_ => 1) } grep { defined } split //, ($as =~ /-?([a-z]+)$/)[0] || '';
push @args, map { @{ $flag_defs{$_} || [] } } keys %flags;

$as =~ s/^5\.//;
my $perl = "5.$as";
$perl =~ s/-?[a-z]+$//; # Strip trailing flags.
my $lib = $as . '@std';

my @test_reports = qw(
  Test::Reporter::Transport::Socket
  CPAN::Reporter
);

my @problem_modules = qw(
);

my @to_install = (
  `cat $Bin/install.txt`,
  `$ENV{HOME}/data/devel/projects/perl/my_dists.pl`
);
chomp(@to_install);

my @no_man = qw/-D man1dir=none -D man3dir=none/;

# Don't prompt me.
close STDIN;

sub run {
  print "run: @_\n";
  system @_; # autodie
}

# install perl and lock it down
run( qw/perlbrew install -j 9 --as/, $as, $perl, @no_man, @args );
run( qw/chmod -R a-w/, "$ENV{HOME}/perl5/perlbrew/perls/$as" );

# give us a local::lib for installing things
run( qw/perlbrew lib create/, $lib );

# let's avoid any pod tests when we try to install stuff
run( qw/perlbrew exec --with/, $lib, qw/cpanm TAP::Harness::Restricted/ );
local $ENV{HARNESS_SUBCLASS} = "TAP::Harness::Restricted";

my $cpan = 'cpan';

# Setup test reports before installing anything else.
run( qw/perlbrew exec --with/, $lib, $cpan, @test_reports );

# some things need forcing
run( qw/perlbrew exec --with/, $lib, $cpan, -f => @problem_modules )
  if @problem_modules;

# Install the rest (retry to catch any circularity problems).
run( qw/perlbrew exec --with/, $lib, $cpan, @to_install )
  for 1, 2;
